plugins {
    id("java")
    id("org.jetbrains.kotlin.jvm") version "1.9.22"
    id("org.jetbrains.intellij.platform") version "2.1.0"
}

group = "org.zeith"
version = file("VERSION.txt").text

repositories {
    mavenCentral()

    intellijPlatform {
        defaultRepositories()
    }
}

dependencies {
    intellijPlatform {
        intellijIdeaCommunity(icVersion)

//        plugins(providers.gradleProperty("platformPlugins").map { it.split(',') })

        bundledPlugins(providers.gradleProperty("platformBundledPlugins").map { List.of(it.split(',')) })

        pluginVerifier()
        zipSigner()
        instrumentationTools()
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

tasks {
    // Set the JVM compatibility versions
    tasks.withType(JavaCompile) {
        sourceCompatibility = "21"
        targetCompatibility = "21"
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions.jvmTarget = "21"
    }

    patchPluginXml {
        sinceBuild.set("242")
        untilBuild.set("244.*")
        changeNotes.set(file("CHANGELOG.txt").text)
    }

    signPlugin {
        certificateChain = file(System.getenv("JBR_SIGN_CHAIN")).text
        privateKey = file(System.getenv("JBR_SIGN_PK")).text
        password = System.getenv("JBR_CH_PASS")
    }

    publishPlugin {
        token = System.getenv("JETBRAINS_MARKETPLACE_TOKEN")
    }
}

tasks.register('uploadPlugin') {
    notCompatibleWithConfigurationCache("Yes.")
    doLast {
        def pluginDir = "R:\\zeith-jbr\\org.zeith.hammerhelper";

        def fn = "hammerhelper-${version}.zip"
        file("build/distributions/${fn}")
                .renameTo(file("${pluginDir}\\${fn}"))

        file("${pluginDir}\\__changelog.txt").setText(file("CHANGELOG.txt").text)
    }
}

afterEvaluate {
    uploadPlugin.dependsOn("buildPlugin")
}